\documentclass[preview,multi,crop=false,border=1in,class=memoir]{standalone}
\usepackage{../common}

\begin{document}
\begin{preview-page}
\section{简单表达式求值}


Erlang解释器在解释Erlang表达式时，会先将表达式转换成另一种形式。这种形
式值求起来更方便。现在可以用 \verb|parse_util:expr/1| 来把一个在Erlang
Shell中输入的表达式转换成这种形式。


整数求值

\begin{ErlangShellSession}
1> parse_util:expr("4.").
{integer,1,4}
2>
\end{ErlangShellSession}


% SNIP REFERENCE eval_integer.erl eval_string
\begin{SourceCode}
% SNIP BEGIN eval_string
eval_string(S, Bindings) ->
    eval(parse_util:expr(S), Bindings).
% SNIP END
\end{SourceCode}


% SNIP REFERENCE eval_integer.erl eval-test-integer
\begin{SourceCode}
% SNIP BEGIN eval-test-integer
test(eval_integer) ->
    {ok, 1, []} = eval_string("1.", []),
    {ok, 2, []} = eval_string("2.", []),
    {ok, 3, []} = eval_string("3.", []),
    ok
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE eval_integer.erl eval-integer
\begin{SourceCode}
% SNIP BEGIN eval-integer
eval({integer, _, Value}, Bindings) ->
    {ok, Value, Bindings}
%- SNIP END
.
\end{SourceCode}


\verb|atom()| 求值

\begin{ErlangShellSession}
1> parse_util:expr("a.").
{atom,1,a}
2>
\end{ErlangShellSession}


% SNIP REFERENCE eval_atom.erl eval-test-atom
\begin{SourceCode}
% SNIP BEGIN eval-test-atom
test(eval_atom) ->
    {ok, a, []} = eval_string("a.", []),
    {ok, b, []} = eval_string("b.", []),
    {ok, c, []} = eval_string("c.", []),
    ok
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE eval_atom.erl eval-atom
\begin{SourceCode}
% SNIP BEGIN eval-atom
eval({atom, _, Value}, Bindings) ->
    {ok, Value, Bindings}
%- SNIP END
.
\end{SourceCode}


变量求值

\begin{ErlangShellSession}
1> parse_util:expr("X.").
{var,1,'X'}
2>
\end{ErlangShellSession}



% SNIP REFERENCE eval_var.erl eval-test-var
\begin{SourceCode}
% SNIP BEGIN eval-test-var
test(eval_var) ->
    {ok, 1, [{'X', 1}]} = eval_string("X.", [{'X', 1}]),
    {error, {unbound, 'X'}} = eval_string("X.", []),
    ok
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE eval_var.erl eval-var
\begin{SourceCode}
% SNIP BEGIN eval-var
eval({var, _, Var}, Bindings) ->
    case bindings:lookup(Var, Bindings) of
        {ok, Value} ->
            {ok, Value, Bindings};
        none ->
            {error, {unbound, Var}}
    end
%- SNIP END
.
\end{SourceCode}


\verb|list()| 求值

\begin{ErlangShellSession}
1> parse_util:expr("[].").
{nil,1}
2> parse_util:expr("[1].").
{cons,1,{integer,1,1},{nil,1}}
3> parse_util:expr("[1,2].").
{cons,1,{integer,1,1},{cons,1,{integer,1,2},{nil,1}}}
4>
\end{ErlangShellSession}


% SNIP REFERENCE eval_list.erl eval-test-list
\begin{SourceCode}
% SNIP BEGIN eval-test-list
test(eval_list) ->
    {ok, [], []} = eval_string("[].", []),
    {ok, [1,2], []} = eval_string("[1,2].", []),
    {ok, [1,2], [{'X', 1}, {'Y', 2}]} = eval_string("[X,Y].", [{'X', 1}, {'Y', 2}]),
    ok
%- SNIP END
.
\end{SourceCode}

% SNIP REFERENCE eval_list.erl eval-list
\begin{SourceCode}
% SNIP BEGIN eval-list
eval({nil, _}, Bindings) ->
    {ok, [], Bindings};
eval({cons, _, H, T}, Bindings) ->
    case eval(H, Bindings) of
        {ok, H1, Bindings1} ->
            case eval(T, Bindings1) of
                {ok, T1, Bindings2} ->
                    {ok, [H1|T1], Bindings2};
                Error ->
                    Error
            end;
        Error ->
            Error
    end
%- SNIP END
.
\end{SourceCode}

\verb|tuple()| 求值

\begin{ErlangShellSession}
1> parse_util:expr("{1,2}.").
{tuple,1,[{integer,1,1},{integer,1,2}]}
2>
\end{ErlangShellSession}


% SNIP REFERENCE eval_tuple.erl eval-test-tuple
\begin{SourceCode}
% SNIP BEGIN eval-test-tuple
test(eval_tuple) ->
    {ok, {tuple, [1,2]}, []} = eval_string("{1,2}.", []),
    {ok, {tuple, [1,2]}, [{'X', 1}, {'Y', 2}]} = eval_string("{X,Y}.", [{'X', 1}, {'Y', 2}]),
    ok
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE eval_tuple.erl eval-tuple
\begin{SourceCode}
% SNIP BEGIN eval-tuple
eval({tuple, _, Elements}, Bindings) ->
    case eval_elements(Elements, Bindings) of
        {ok, Value, Bindings1} ->
            {ok, {tuple, Value}, Bindings1};
        Error ->
            Error
    end
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE eval_tuple.erl eval_elements
\begin{SourceCode}
% SNIP BEGIN eval_elements
eval_elements([], Bindings) ->
    {ok, [], Bindings};
eval_elements([H|T], Bindings) ->
    case eval(H, Bindings) of
        {ok, H1, Bindings1} ->
            case eval_elements(T, Bindings1) of
                {ok, T1, Bindings2} ->
                    {ok, [H1|T1], Bindings2};
                Error ->
                    Error
            end;
        Error ->
            Error
    end.
% SNIP END
\end{SourceCode}


匹配整数

% SNIP REFERENCE match_integer.erl eval-match
\begin{SourceCode}
% SNIP BEGIN eval-match
eval({match, _, A, B}, Bindings) ->
    case eval(B, Bindings) of
        {ok, Value, Bindings1} ->
            match(A, Value, Bindings1);
        Error ->
            Error
    end
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE match_integer.erl match-test-integer
\begin{SourceCode}
% SNIP BEGIN match-test-integer
test(match_integer) ->
    {ok, 1, []} = eval_string("1 = 1.", []),
    {error, {mismatch, 2}} = eval_string("1 = 2.", []),
    ok
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE match_integer.erl match-integer
\begin{SourceCode}
% SNIP BEGIN match-integer
match({integer, _, Value}, Value, Bindings) ->
    {ok, Value, Bindings};
match({integer, _, _}, Value, _) ->
    {error, {mismatch, Value}}
%- SNIP END
.
\end{SourceCode}


匹配\verb|atom()|

% SNIP REFERENCE match_atom.erl match-test-atom
\begin{SourceCode}
% SNIP BEGIN match-test-atom
test(match_atom) ->
    {ok, a, []} = eval_string("a = a.", []),
    {error, {mismatch, b}} = eval_string("a = b.", []),
    ok
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE match_atom.erl match-atom
\begin{SourceCode}
% SNIP BEGIN match-atom
match({atom, _, Value}, Value, Bindings) ->
    {ok, Value, Bindings};
match({atom, _, _}, Value, _) ->
    {error, {mismatch, Value}}
%- SNIP END
.
\end{SourceCode}


匹配变量

% SNIP REFERENCE match_var.erl match-test-var
\begin{SourceCode}
% SNIP BEGIN match-test-var
test(match_var) ->
    {ok, a, [{'X', a}]} = eval_string("X = a.", []),
    {ok, a, [{'X', a}]} = eval_string("X = a.", [{'X', a}]),
    {error, {mismatch, b}} = eval_string("X = a.", [{'X', b}]),
    ok
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE match_var.erl match-var
\begin{SourceCode}
% SNIP BEGIN match-var
match({var, _, Var}, Value, Bindings) ->
    case bindings:lookup(Var, Bindings) of
        {ok, Value} ->
            {ok, Value, Bindings};
        {ok, Value2} ->
            {error, {mismatch, Value2}};
        none ->
            {ok, Value, [{Var,Value}|Bindings]}
    end
%- SNIP END
.
\end{SourceCode}

匹配\verb|list()|

% SNIP REFERENCE match_list.erl match-test-list
\begin{SourceCode}
% SNIP BEGIN match-test-list
test(match_list) ->
    {ok, [], []} = eval_string("[] = [].", []),
    {error, {mismatch, 1}} = eval_string("[] = 1.", []),
    {ok, [1,2,3], []} = eval_string("[1,2,3] = [1,2,3].", []),
    {error, {mismatch, [1,2]}} = eval_string("[1,2,3] = [1,2].", []),
    ok
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE match_list.erl match-list
\begin{SourceCode}
% SNIP BEGIN match-list
match({nil, _}, [], Bindings) ->
    {ok, [], Bindings};
match({nil, _}, Value, _) ->
    {error, {mismatch, Value}};
match({cons, _, H, T}, [VH|VT], Bindings) ->
    case match(H, VH, Bindings) of
        {ok, H1, Bindings1} ->
            case match(T, VT, Bindings1) of
                {ok, T1, Bindings2} ->
                    {ok, [H1|T1], Bindings2};
                {error, {mismatch, _}} ->
                    {error, {mismatch, [VH|VT]}};
                Error ->
                    Error
            end;
        {error, {mismatch, _}} ->
            {error, {mismatch, [VH|VT]}};
        Error ->
            Error
    end;
match({cons, _, _, _}, Value, _) ->
    {error, {mismatch, Value}}
%- SNIP END
.
\end{SourceCode}

匹配\verb|tuple()|

% SNIP REFERENCE match_tuple.erl match-test-tuple
\begin{SourceCode}
% SNIP BEGIN match-test-tuple
test(match_tuple) ->
    {ok, {tuple, []}, []} = eval_string("{} = {}.", []),
    {error, {mismatch, 1}} = eval_string("{} = 1.", []),
    {ok, {tuple, [1,2,3]}, []} = eval_string("{1,2,3} = {1,2,3}.", []),
    {error, {mismatch, {tuple, [1,2]}}} = eval_string("{1,2,3} = {1,2}.", []),
    ok
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE match_tuple.erl match-tuple
\begin{SourceCode}
% SNIP BEGIN match-tuple
match({tuple, _, Elements}, {tuple, Values}, Bindings) ->
    case match_elements(Elements, Values, Bindings) of
        {ok, Value, Bindings1} ->
            {ok, {tuple, Value}, Bindings1};
        {error, {mismatch, _}} ->
            {error, {mismatch, {tuple, Values}}};
        Error ->
            Error
    end;
match({tuple, _, _}, Value, _) ->
    {error, {mismatch, Value}}
%- SNIP END
.
\end{SourceCode}


% SNIP REFERENCE match_tuple.erl match_elements
\begin{SourceCode}
% SNIP BEGIN match_elements
match_elements([], [], Bindings) ->
    {ok, [], Bindings};
match_elements([H|T], [VH|VT], Bindings) ->
    case match(H, VH, Bindings) of
        {ok, H1, Bindings1} ->
            case match_elements(T, VT, Bindings1) of
                {ok, T1, Bindings2} ->
                    {ok, [H1|T1], Bindings2};
                {error, {mismatch, _}} ->
                    {error, {mismatch, [VH|VT]}};
                Error ->
                    Error
            end;
        {error, {mismatch, _}} ->
            {error, {mismatch, [VH|VT]}};
        Error ->
            Error
    end;
match_elements(_, Value, _) ->
    {error, {mismatch, Value}}.
% SNIP END
\end{SourceCode}

\end{preview-page}
\end{document}
